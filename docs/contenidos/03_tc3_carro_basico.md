# üöÉ Carro b√°sico (TwinCAT 3)

<!-- 
!!! info "Info"
    Enlace al repositorio en Github [![Github Logo](../images/_resource/github_logo.svg){width=15px}](https://github.com/vetorres-uma/TC3_Carro_Basico)
-->

## üìù Descripci√≥n del Proyecto

El **carro va y viene** es un m√≥vil que se desplaza longitudinalmente entre los extremos izquierdo y derecho de un tramo de v√≠a.

![Esquematico del Carro B√°sico](../images/03_tc3_carro_basico/Carro_Basico_Esquematico.png){width=350px}

### üß© Elementos constituyentes

La **parte operativa** del carro b√°sico est√° constituida por los siguiente dispositivos:

- Un **motor** con dos se√±ales de mando (izquierda y derecha)
- Un par de **sensores finales de carrera** (izquierdo y derecho)

La **parte de relaci√≥n** consiste en un panel de operador b√°sico compuesto √∫nicamente por:

- Un **pulsador de marcha**.
- Una **l√°mpara de marcha**.

### üîÑ Descripci√≥n del proceso

El funcionamiento del carro b√°sico es como sigue.

1. El carro se pone en marcha hacia la derecha cuando se acciona el pulsador de marcha. 
1. Cuando el carro alcanza el final de carrera derecha invierte el sentido de la marcha.
1. El carro se deteine al alcanzar, de nuevo,  el final de carrera izquierda (posici√≥n inicial).

- **Condici√≥n inicial**: carro detenido sobre el final de carrera izquierda.

### üî¢ Modalidades

1. **Carro pulsado**. El carro inicia un viaje de ida y vuelta, √∫nicamente, cuando estando en su posici√≥n inicial se acciona el pulsador de marcha.
1. **Carro temporizado**. El carro se detiene durante un determinado tiempo sobre el final de carrera derecha antes de iniciar el camino de regreso hacia su posici√≥n inicial.
1. **Carro limitado**. El carro realiza un determinado n√∫mero de viajes de ida y vuelta (tarea) cada vez que, estando en su posici√≥n inicial, se acciona el pulsador de marcha.
1. **Carro se√±alizado**. La l√°mpara de marcha se enciende de forma permanente para indicar que el carro est√° en funcionamiento y parpadea para indicar que el carro est√° en reposo.

### ‚áÑ Entradas y salidas

| Nombre | Tipo | Origen | Descripci√≥n |
| :--- | :--- | :--- | :--- |
| `PM` | `BOOL` | Input | Pulsador de Marcha |
| `FCI` | `BOOL` | Input | Final de Carrera Izquierda |
| `FCD` | `BOOL` | Input | Final de Carrera Derecha |
| `LM` | `BOOL` | Output | Lampara de Marcha |
| `MI` | `BOOL` | Output | Marcha Izquierda |
| `MD` | `BOOL` | Output | Marcha Derecha |

---

### üìÑ Especificaci√≥n funcional

Las siguientes especificaciones funcionales describen el comportamiento del carro (l√≥gica de control) de una manera precisa utilizando los diagramas de rel√©s y contactos y el lenguaje GRAFCET.

- [Diagrama de rel√©s y contactos (PDF)](../../pdfs/Carro_Basico_DRC.pdf)
- [Diagrama grafcet (PDF)](../../pdfs/Carro_Basico_GRF.pdf)

---

### üíª C√≥digo

Implementa el funcionamiento b√°sico de este "famoso" problema de automatizaci√≥n del carro va y viene en sus diferentes modalidades (b√°sico, pulsado, temporizado, limitado y se√±alizado).

Una de las caracter√≠stica m√°s relevante de este proyecto did√°ctico es que se muestran diferentes formas de especificar e implementer un problema simple de automatizaci√≥n, empleando el lenguaje de especificaci√≥n GRAFCET y usando diferentes lenguajes de programaci√≥n de la norma IEC 61131-3 (`SFC` y `ST`).

- GRF ‚Üí [SFC / ST]

---

## üíª Requisitos del Sistema

### Software

- **IDE:** Microsoft Visual Studio / TwinCAT 3 XAE (Versi√≥n m√≠nima recomendada: **3.1.4024.x**).
- **Lenguajes:** Texto Estructurado (ST) y Diagrama de Funciones Secuenciales (SFC).

## üî® Replicar el proyecto

### Informaci√≥n inicial

!!! warning "Importante"
    El proyecto completo que se explica aqu√≠ se corresponde con la versi√≥n **se√±alizada** del carro va y viene:
    
    - El carro inicia un viaje de ida y vuelta, √∫nicamente, cuando estando en su posici√≥n inicial se acciona el pulsador de marcha (se utiliza la detecci√≥n del flanco de subida).
    - El carro se detiene durante un determinado tiempo sobre el final de carrera derecha antes de iniciar el camino de regreso hacia su posici√≥n inicial.
    - Este proceso de ida y vuelta se realiza un determinado n√∫mero de veces (configurable en la visualizaci√≥n) con una sola acci√≥n del pulsador de marcha.
    - La l√°mpara de marcha se enciende de forma permanente para indicar que el carro est√° en funcionamiento y parpadea para indicar que el carro est√° en reposo.

**Para replicar la creaci√≥n de la soluci√≥n completa, seguir este procedimiento:**

!!! tip "Sugerencia"
    Pulsa en ‚û°Ô∏è para obtener m√°s informaci√≥n sobre c√≥mo realizar el paso especificado.

1. Crear una soluci√≥n de TwinCAT3 con nombre `tc3_carro_basico` [‚û°Ô∏è](../../contenidos/01_conceptos/#crear-proyecto-tc3)
2. Crear un proyecto PLC con nombre `carro_basico_PLC` [‚û°Ô∏è](../../contenidos/01_conceptos/#crear-proyecto-plc)
3. Escoger un lenguaje para la implementaci√≥n: **ST** o **SFC**.
---

### Versi√≥n en ST

!!! info "Informaci√≥n"
    En esta versi√≥n conoceremos c√≥mo implementar una m√°quina de estados en lenguaje **ST**. 
    
    Para ello, vamos a necesitar una variable espec√≠fica que guarde el valor actual del estado: `Estado`, que
    ser√° de tipo `ENUM` y podr√° tomar los siguientes valores:
    
    `E_Reposo`, `E_MarchandoDerecha`, `E_EsperandoDerecha`, `E_MarchandoIzquierda`, `E_EvaluandoTarea`.
    
    N√≥tese que estos estados se corresponden con los que tenemos especificados en el **GRAFCET** del carro se√±alizado.

1. Crear un bloque funcional con nombre `FB_Carro_ST` [‚û°Ô∏è](../../contenidos/01_conceptos/#crear-bloque-funcional)
2. Declarar las variables dentro del FB [‚û°Ô∏è](../../contenidos/01_conceptos/#declaracion-de-variables)
    - **Entrada** (se puede especificar su valor en la llamada al FB)
    
    ```st
    VAR_INPUT
        ManiobrasSolicitadas     : UINT := 2;
        TiempoEspera             : TIME := T#2S;
    END_VAR
    ```

    | Variable              | Tipo | E/S | Descripci√≥n |
    |-----------------------|------|-----|-------------|
    | ManiobrasSolicitadas  | UINT | no  | N¬∫ de ciclos "va-y-viene" solicitadas para ejecutar. |
    | TiempoEspera          | TIME | no  | Tiempo de espera en lado derecho. |
   
    - **Salida** (se puede utilizar su valor fuera del FB)
    
    ```st
    VAR_OUTPUT
        ManiobrasPendientes      : UINT;
        TiempoPendiente          : TIME;
    END_VAR
    ```

    | Variable              | Tipo | E/S | Descripci√≥n |
    |-----------------------|------|-----|-------------|
    | ManiobrasPendientes   | UINT | no  | N¬∫ de ciclos que quedan por realizar |
    | TiempoPendiente       | TIME | no  | Tiempo restante de la espera/temporizaci√≥n |

    - **Locales** (de uso interno al FB)
    
    ```st
    VAR
        // Enumeracion de etiquetas para los estados
        Estado : (E_Reposo, E_MarchandoDerecha, E_EsperandoDerecha, E_MarchandoIzquierda, E_EvaluandoTarea);
        
        // Utilidades
        BLK                      : FB_Blink;
        FlancoPulsadorMarcha     : R_TRIG;
        TemporizadorEspera       : TON;
        
        // Entradas
        i_FinalDerecha    AT %I* : BOOL;
        i_FinalIzquierda  AT %I* : BOOL := TRUE;
        i_PulsadorMarcha  AT %I* : BOOL;
        
        // Salidas
        o_LamparaMarcha   AT %Q* : BOOL;
        o_MarchaDerecha   AT %Q* : BOOL;
        o_MarchaIzquierda AT %Q* : BOOL;
    END_VAR
    ``` 

    | Variable | Tipo | E/S | Descripci√≥n |
    |----------|------|-----|-------------|
    | **Estado**              | ENUM | no | Estado actual de la m√°quina de estados del carro |
    | BLK                     | FB_Blink | no  | Bloque auxiliar para hacer parpadear una se√±al (p.ej. l√°mpara) |
    | FlancoPulsadorMarcha    | R_TRIG | no | Detector de flanco ascendente del pulsador de marcha |
    | TemporizadorEspera      | TON  | no | Temporizador de retardo para gestionar la espera |
    | i_FinalDerecha          | BOOL | <span class="fondo-amarillo">**E**</span> | Final de carrera derecha (entrada digital) |
    | i_FinalIzquierda        | BOOL | <span class="fondo-amarillo">**E**</span> | Final de carrera izquierda (entrada digital) |
    | i_PulsadorMarcha        | BOOL | <span class="fondo-amarillo">**E**</span> | Pulsador de inicio/marcha del ciclo |
    | o_LamparaMarcha         | BOOL | <span class="fondo-rojo">**S**</span> | L√°mpara indicadora de marcha/funcionamiento |
    | o_MarchaDerecha         | BOOL | <span class="fondo-rojo">**S**</span> | Orden de movimiento hacia la derecha (salida digital) |
    | o_MarchaIzquierda       | BOOL | <span class="fondo-rojo">**S**</span> | Orden de movimiento hacia la izquierda (salida digital) |

3. Escribir el c√≥digo
    
    El c√≥digo va a estar formado por tres regiones que se implementan todas en la caja de c√≥digo: una para la llamada a las **utilidades**, otra para la **funci√≥n de estado** y otra para la **funci√≥n de salida**.
    
    1. Llamada a los FBs de **utilidades**:

        ```st
        // --- UTILIDADES ---
        BLK();
        FlancoPulsadorMarcha(CLK := i_PulsadorMarcha);
        TemporizadorEspera(IN := (Estado = E_EsperandoDerecha), PT := TiempoEspera);
        ``` 

        - Genera la se√±al de parpadeo con `BLK`.
        - Monitoriza el flanco de subida de `i_PulsadorMarcha`
        - Activa un temporizador de `TiempoEspera` cuando el estado `E_EsperandoDerecha` est√© activo.
    

    2. Especificaci√≥n de la **funci√≥n de estado** que implementa la evoluci√≥n de la secuencia:

        ```st
        // --- FUNCION DE ESTADO ---
        CASE Estado OF
            E_Reposo:
                IF i_FinalIzquierda AND FlancoPulsadorMarcha.Q THEN
                    // Accion a la salida
                    IF ManiobrasPendientes = 0 THEN
                        ManiobrasPendientes := ManiobrasSolicitadas;
                    END_IF;
                    
                    Estado := E_MarchandoDerecha;
                END_IF;
                
            E_MarchandoDerecha:
                IF i_FinalDerecha THEN
                    Estado := E_EsperandoDerecha;
                END_IF;
                
            E_EsperandoDerecha:
                // Accion principal
                TiempoPendiente := TiempoEspera - TemporizadorEspera.ET;
                
                IF TemporizadorEspera.Q THEN
                    Estado := E_MarchandoIzquierda;
                END_IF;
                
            E_MarchandoIzquierda:
                IF i_FinalIzquierda THEN
                    // Accion a la entrada (siguiente estacion)
                    IF ManiobrasPendientes > 0 THEN
                        ManiobrasPendientes := ManiobrasPendientes - 1;
                    END_IF;
                    
                    Estado := E_EvaluandoTarea;            
                END_IF;
                
            E_EvaluandoTarea:
                IF ManiobrasPendientes = 0 THEN
                    Estado := E_Reposo;
                ELSIF ManiobrasPendientes > 0 THEN
                    Estado := E_MarchandoDerecha;
                END_IF;
        END_CASE;
        ```
        
        - En **reposo**: Pasa a `E_MarchandoDerecha` si `i_FinalIzquierda` est√° activo y se detecta el flanco de `i_PulsadorMarcha`. Antes de salir de este estado, inicializa el contador `ManiobrasPendientes` a `ManiobrasSolicitadas`.
        - **Marchando hacia la derecha**: Pasa a `E_EsperandoDerecha` cuando se active el sensor fin de carrera a la derecha `i_FinalDerecha`.
        - **Esperando en la derecha**: Calcula el tiempo restante del temporizador y lo asigna a `TiempoRestante`. Cuando el temporizador termine, pasa a `E_MarchandoIzquierda`.
        - **Marchando hacia la izquierda**: Monitoriza el valor del sensor fin de carrera izquierdo `i_FinalIzquierda`. Cuando se active, decrementa el contador de maniobras pendientes y pasa al estado de evaluaci√≥n de la tarea.
        - **Evaluando la tarea**: Determina si el contador de maniobras pendientes ha llegado a cero, en cuyo caso volvemos a reposo. En caso contrario, repite el proceso "va-y-viene" pasando a `E_MarchandoDerecha`.

    3. Especificaci√≥n de la **funci√≥n de salida** que activa las salidas del sistema seg√∫n el estado activo:
       
        ```st
        // --- FUNCION DE SALIDA ---
        o_LamparaMarcha    := ((Estado = E_Reposo) AND BLK.Q) OR (Estado <> E_Reposo);
        o_MarchaDerecha    := (Estado = E_MarchandoDerecha);
        o_MarchaIzquierda  := (Estado = E_MarchandoIzquierda);
        ```

        - Enciende la l√°mpara (`o_LamparaMarcha`) intermitentemente en el estado de reposo y fija en cualquier otro caso.
       
            !!! info "Informaci√≥n"
                N√≥tese que `((Estado = E_Reposo) AND BLK.Q)` genera una se√±al que conmuta intermitentemente cuando el estado de reposo est√© activo.
       
        - Activa la salida digital que propicia el movimiento del motor hacia la derecha `o_MarchaDerecha` en el estado de marcha hacia la derecha.
        - Activa la salida digital que propicia el movimiento del motor hacia la izquierda `o_MarchaIzquierda` en el estado de marcha hacia la izquierda.
        
---

### Versi√≥n en SFC

!!! info "Informaci√≥n"
    En esta versi√≥n conoceremos c√≥mo implementar una m√°quina de estados en lenguaje gr√°fico **SFC**.

    Este lenguaje es muy √∫til para implementar m√°quinas de estados de manera sencilla ya que su codificaci√≥n explicita directamente la secuencia de estados y sus transiciones.
    
    N√≥tese la gran similitud entre la especificaci√≥n en **GRAFCET** y la implementaci√≥n en **SFC**.

1. Crear un bloque funcional con nombre `FB_Carro_SFC` [‚û°Ô∏è](../../contenidos/01_conceptos/#crear-bloque-funcional)
2. Declarar las variables dentro del FB [‚û°Ô∏è](../../contenidos/01_conceptos/#declaracion-de-variables)

    !!! info "Informaci√≥n"
        Las variables para la versi√≥n **SFC** son las mismas que en la versi√≥n **ST** con dos excepciones:
            
        - **No necesitamos una variable de estado**, ya que viene impl√≠cito en el lenguaje. Adem√°s, TwinCAT 3 asocia una variable booleana a cada estado que indica si √©ste est√° activo: `[nombre_etapa].x`.
        - **No necesitamos el temporizador** ya que TwinCAT 3 incorpora un temporizador asociado a cada etapa, al que se puede acceder mediante el c√≥digo `[nombre_etapa].t`.

3. Implementamos el c√≥digo en SFC [‚û°Ô∏è](../../contenidos/01_conceptos/#lenguaje-sfc)
    
    ![C√≥digo de carro b√°sico SFC](../images/03_tc3_carro_basico/carro_basico_sfc.png){width=500px}

    ---
    
    Explicaci√≥n del c√≥digo (revisar los tipos de acciones [‚û°Ô∏è](../../contenidos/01_conceptos/#asociar-acciones-a-etapas)):

    - Etapa `S0`. Se realizan tres acciones asociadas:
        - Acci√≥n **continua** `a_LamparaMarcha`: Hace que parpadee cuando `S0` est√© activo (`S0.x = TRUE`) y quede fija en cualquier otra etapa.
        
        ```st
        BLK();
        o_LamparaMarcha := (S0.x AND BLK.Q) OR NOT S0.x;
        ```
        
        - Acci√≥n **principal** `a_FlancoMarcha`: Realiza la llamada al FB de detecci√≥n de flanco de subida del pulsador de marcha. Es en este estado donde queremos que se monitorice este flanco.
        
        ```st
        FlancoPulsadorMarcha(CLK := i_PulsadorMarcha);
        ```

        - Acci√≥n **memorizada a la salida** `a_ManiobrasPendientes_Iniciar`: Antes de pasar a la siguiente etapa, inicializamos el valor de la variable `ManiobrasPendientes` al valor solicitado en `ManiobrasSolicitadas`.
        
        ```st
        IF ManiobrasPendientes = 0 THEN
            ManiobrasPendientes := ManiobrasSolicitadas;
        END_IF
        ```
    
    ---

    - Transici√≥n `S0` ‚Üí `S1`: Cuando est√© activo el sensor fin de carrera izquierdo y se produzca el flanco de subida del pulsador de marcha.
    
    ---
    
    - Etapa `S1`. Se realizan dos acciones asociadas:
        - Acci√≥n **continua** que activa la salida digital correspondiente al movimiento del motor hacia la derecha: `o_MarchaDerecha`.
        - Acci√≥n **continua** `a_LamparaMarcha`.
    
    ---
    
    - Transici√≥n `S1` ‚Üí `S2`: Cuando est√© activo el sensor fin de carrera derecho.

    ---

    - Etapa `S2`. Se realizan dos acciones asociadas:
        - Acci√≥n **continua** `a_LamparaMarcha`.
        - Acci√≥n **principal** `a_TiempoEspera`: Calcula el tiempo que queda por esperar en la posici√≥n derecha.

        ```st
        TiempoPendiente := TiempoEspera - S2.t;
        ```
        
        !!! info "Informaci√≥n"
            N√≥tese el uso de la expresi√≥n `S2.t`, donde se va almacenando, de forma autom√°tica por TwinCAT 3, el tiempo que la etapa `S2` est√° activa.

    ---
    
    - Transici√≥n `S2` ‚Üí `S3`: Cuando el tiempo que la etapa `S2` est√° activa iguale o supere al tiempo de espera: `S2.t >= TiempoEspera`

    ---
    
    - Etapa `S3`. Se realizan dos acciones asociadas:
        - Acci√≥n **continua** que activa la salida digital correspondiente al movimiento del motor hacia la izquierda: `o_MarchaIzquierda`.
        - Acci√≥n **continua** `a_LamparaMarcha`.

    ---
    
    - Transici√≥n `S3` ‚Üí `S4`: Cuando est√© activo el sensor fin de carrera izquierdo.

    ---
   
    - Etapa `S4`. Se realizan dos acciones asociadas:
        - Acci√≥n **continua** `a_LamparaMarcha`.
        - Acci√≥n **memorizada a la entrada** `a_ManiobrasPendientes_Iniciar`. Justo al entrar en esta etapa, se decrementa el valor de las `ManiobrasPendientes`.

        ```st
        IF ManiobrasPendientes > 0 THEN
            ManiobrasPendientes := ManiobrasPendientes - 1;
        END_IF
        ```

    ---
    
    - Bifurcaci√≥n condicional
        - Transici√≥n `S4` ‚Üí `S0`: Si el n√∫mero de maniobras pendientes ha llegado a cero, volvemos a la etapa inicial.
        - Transici√≥n `S4` ‚Üí `S1`: Si el n√∫mero de maniobras pendientes a√∫n no ha llegado a cero, repetimos el proceso.
  
    ---

4. Dise√±ar la visualizaci√≥n a√±adiendo: [‚û°Ô∏è](../../contenidos/01_conceptos/#crear-visualizacion)

    ![Imagen](../images/03_tc3_carro_basico/carro_basico_visu.png){width=400px}

    1. Rect√°ngulos (*Rectangle*) para las etiqueta **Panel**, **Maniobras**, **Tiempos**, etc.
    2. Rect√°ngulos (*Rectangle*) **editables** para introducir el valor de `ManiobrasSolicitadas` y `TiempoEspera`.
        
        ??? info "Par√°metros"
            - Texts > Text = 
                - [%d] *Formato estilo printf que indica que se va a sustituir por un n√∫mero entero*
                - [%t] *Formato estilo printf que indica que se va a sustituir por una variable tipo `TIME`* 
            - Text variables > Text variable = 
                - [`MAIN.Carro.ManiobrasSolicitadas`]
                - [`MAIN.Carro.TiempoEspera`]
            - Inputconfiguration 
                - OnMouseClick > Configure > Write a Variable. 
                    - *Aceptar cuadro de di√°logo por defecto. El valor introducido se escribir√° en la variable especificada en Text Variable.*

    3. Rect√°ngulos (*Rectangle*) **no editables** para mostrar el valor de `ManiobrasPendientes` y `TiempoRestante`.

        ??? info "Par√°metros"
            - Texts > Text = 
                - [%d] *Formato estilo printf que indica que se va a sustituir por un n√∫mero entero*
                - [%t] *Formato estilo printf que indica que se va a sustituir por una variable tipo `TIME`* 
            - Text variables > Text variable = 
                - [`MAIN.Carro.ManiobrasPendientes`]
                - [`MAIN.Carro.TiempoRestante`]

    4. Rect√°ngulos (*Rectangle*) para las variables booleanas correspondientes a la l√°mpara, pulsador, sensores final de carrera y activaci√≥n de motores. **Tanto para mostrar su valor como para poder modificarlo.**

        ??? info "Par√°metros"
            - Texts > Text = [**Pulsador**], [**Lampara**], , etc.
            - Color variables > Toggle color = [`MAIN.i_PulsadorMarcha`], [`MAIN.o_Lampara`], etc.
            - InputConfiguration
                - Toggle > Variable: [`MAIN.i_PulsadorMarcha`], [`MAIN.o_Lampara`], etc.


6. Declarar la variable `Carro` de tipo `FB_Carro_SFC` o `FB_Carro_ST` en el programa `MAIN`, seg√∫n la versi√≥n a utilizar.
    
    ```st
    PROGRAM MAIN
    VAR
        Carro: FB_Carro_SFC; // o Carro: FB_Carro_ST;
    END_VAR
    ```

7. Escribir, en la zona de implementaci√≥n de `MAIN`, la llamada al FB del `Carro`.

    ```st
    Carro();
    ```

## üöÄ Puesta en Marcha

!!! info "Lenguaje"
    Se proporciona con implementaciones equivalentes en `ST` y en `SFC`.

**Para descargar, compilar y ejecutar este proyecto en el entorno de TwinCAT 3, seguir una de estas dos opciones:**

- Mediante el Campus Virtual
- Mediante GIT
 
### Mediante el Campus Virtual

1. **Copiar** a tu equipo local el fichero `CV > Automatizaci√≥n > ejemplos > 2_tc3_carro_basico > tc3_carro_basico.tnzip` que hay en la carpeta del campus virtual.
2. **Seguir el procedimiento** descrito [aqu√≠](../../contenidos/01_conceptos/#abrir-un-fichero-tnzip) para generar la **Soluci√≥n** a partir del fichero.

### Mediante GIT

1. **Clonar el Repositorio:**

```bash
git clone https://github.com/vetorres-uma/TC3_Carro_Basico.git
```

2. **Abrir el Proyecto:** abra el archivo `.sln` (Soluci√≥n) ubicado en la carpeta principal utilizando el entorno de ingenier√≠a **TwinCAT XAE** (integrado en Visual Studio).
1. **Selecci√≥n del Controlador:** seleccione el simulador (**UmRT_Default**) o controlador local o remoto (**Choose Runtime System**).
1. **Activaci√≥n de Configuraci√≥n:** en el modo **Configuraci√≥n**, active la configuraci√≥n (**Activate Configuration**)) y reinicie TwinCAT en modo **Ejecuci√≥n (Run Mode)**.
1. **Carga del C√≥digo:** en el entorno PLC, inicie la sesi√≥n y descargue el programa al PLC (**Login**).
1. **Poner el c√≥digo en ejecuci√≥n:** ejecute la l√≥gica de control en el controlador (**Start**). Puede utilizar la visualizaci√≥n integrada en el proyecto PLC para facilitar la prueba.

---

## ü§ù Contribuciones

Este proyecto es utilizado con fines educativos. Las contribuciones, sugerencias o correcciones de errores son bienvenidas. Por favor, abra un *Issue* o env√≠e un *Pull Request* si desea contribuir.

---

## üßë Autor

- **Autor Principal:** Victor Torres ([@vetorres-uma](https://github.com/vetorres-uma>))
- **Revisor**: Francisco √Ångel Moreno ([@famoreno](https://github.com/famoreno))

---

## ‚öñÔ∏è Licencia

Este proyecto es de c√≥digo abierto y est√° disponible bajo la **Licencia P√∫blica General GNU (GPL)**.

- Consulte el archivo `LICENSE.md` para m√°s detalles.